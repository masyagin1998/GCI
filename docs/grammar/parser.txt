unit                    = function_decl*

function_decl           = FUNCTION IDENT LPAREN formal_parameters_list RPAREN body |
                          FUNCTION IDENT LPAREN RPAREN body

formal_parameters_list  = IDENT (COMMA IDENT)* RPAREN

body                    = LBRACE statement* RBRACE

statement               = decl_statement |
                          assign_statement |
                          function_call_statement |
                          if_statement |
                          while_statement |
                          break_statement |
                          continue_statement |
                          return_statement

decl_statement          = LET IDENT EQ assignment_expr SEMI

assign_statement        = variable EQ assignment_expr SEMI

function_call_statement = function_call SEMI

if_statement            = IF LPAREN logical_or_expr RPAREN body ELSE if_statement |
                          IF LPAREN logical_or_expr RPAREN body ELSE body |
                          IF LPAREN logical_or_expr RPAREN body

while_statement         = WHILE LPAREN logical_or_expr RPAREN body

break_statement         = BREAK SEMI

continue_statement      = CONTINUE SEMI

return_statement        = RETURN assignment_expr SEMI |
                          RETURN SEMI

variable                = IDENT (variable_part)*
variable_part           = DOT IDENT |
                          LBRACKET logical_or_expr RBRACKET

assignment_expr         = object_literal | array_literal | logical_or_expr

object_literal          = LBRACE properties_list RBRACE |
                          LBRACE RBRACE

array_literal           = LBRACKET args_list RBRACKET |
                          LBRACKET RBRACKET

properties_list         = property (COMMA property)*
property                = IDENT COLON assignment_expr

logical_or_expr         = logical_and_expr (OR logical_and_expr)*
logical_and_expr        = eq_expr (AND eq_expr)*
eq_expr                 = relational_expr (eq_op relationa_expr)?
eq_op                   = EQEQ | NEQ
relational_expr         = additive_expr (relational_op additive_expr)?
relational_op           = LT | GT | LE | GE
additive_expr           = multiplicative_expr (additive_op multiplicative_expr)*
additive_op             = PLUS | MINUS
multiplicative_expr     = left_unary_expr | (multiplicative_op left_unary_expr)*
multiplicative_op       = MUL | DIV | MOD
left_unary_expr         = left_unary_op primary_expr | primary_expr
left_unary_op           = PLUS | MINUS
primary_expr            = function_call | variable | NUMBER | LPAREN logical_or_expr RPAREN

function_call           = IDENT LPAREN args_list RPAREN |
                          IDENT LPAREN RPAREN
args_list               = assignment_expr (COMMA assignment_expr)*

                         
